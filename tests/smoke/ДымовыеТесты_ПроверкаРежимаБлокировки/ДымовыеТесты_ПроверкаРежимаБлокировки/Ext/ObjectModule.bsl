Перем КонтекстЯдра;
Перем Ожидаем, ИтераторМетаданных;

Перем НаборТестов;

#Область Стандартный_интерфейс

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	ИтераторМетаданных = КонтекстЯдра.Плагин("ИтераторМетаданных");
	
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестовПараметр, КонтекстЯдраПараметр) Экспорт
	
	НаборТестов = НаборТестовПараметр;
	Инициализация(КонтекстЯдраПараметр);
	ИтераторМетаданных.Инициализация(КонтекстЯдраПараметр);
	
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.Документы);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.БизнесПроцессы);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.Задачи);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.Справочники);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.РегистрыСведений);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.Константы);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.РегистрыНакопления);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.РегистрыРасчета);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.РегистрыБухгалтерии);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.ПланыВидовХарактеристик);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.ПланыВидовРасчета);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.Последовательности);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.ВнешниеИсточникиДанных);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.WebСервисы);
	
	ИтераторМетаданных.Перечислить(ЭтотОбъект, "ПриСледующемОбъектеМетаданных", "ПриСледующемОбъектеМетаданных");
	
КонецПроцедуры

Процедура ПриСледующемОбъектеМетаданных(ОбъектМетаданных, Родитель) Экспорт
	
	Если Родитель=Неопределено И ТипЗнч(ОбъектМетаданных)=Тип("Строка") Тогда 
		
		НаборТестов.НачатьГруппу("Проверка режима блокировки данных " + ОбъектМетаданных);
		
	ИначеЕсли ОбъектМетаданных<>Неопределено Тогда 
		
		ПолноеИмяОбъекта = ОбъектМетаданных.ПолноеИмя();
		Если СтрНачинаетсяС(ПолноеИмяОбъекта, "WebСервис") Тогда 
			Для Каждого Операция Из ОбъектМетаданных.Операции Цикл 
				ПараметрыТеста = НаборТестов.ПараметрыТеста(Операция, Родитель);
				НаборТестов.Добавить("Тест_ПроверитьРежимБлокировкиОбъекта", ПараметрыТеста, "" + ОбъектМетаданных.ПолноеИмя() + "." + Операция.Имя);
			КонецЦикла;
		Иначе
			ПараметрыТеста = НаборТестов.ПараметрыТеста(ОбъектМетаданных, Родитель);
			НаборТестов.Добавить("Тест_ПроверитьРежимБлокировкиОбъекта", ПараметрыТеста, "" + ОбъектМетаданных.ПолноеИмя());
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Тест_ПроверитьРежимБлокировкиОбъекта(ОбъектМетаданных, Родитель) Экспорт
	
	РежимПроверен = ОбъектМетаданных.РежимУправленияБлокировкойДанных=Метаданные.РежимУправленияБлокировкойДанных;
	Ожидаем.Что(РежимПроверен).ЕстьИстина();
	
КонецПроцедуры

Процедура ПередЗапускомТеста() Экспорт

КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

#КонецОбласти

