#Область ОписаниеПеременных

&НаКлиенте
Перем КонтекстЯдра;
&НаКлиенте
Перем Утверждения;
&НаКлиенте
Перем СтроковыеУтилиты;
&НаКлиенте
Перем ОсновныеРоли;
&НаКлиенте
Перем СтандартныеРоли;
&НаКлиенте
Перем ИсключенияИзПроверок;

#КонецОбласти

#Область ИнтерфейсТестирования

&НаКлиенте
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	СтроковыеУтилиты = КонтекстЯдра.Плагин("СтроковыеУтилиты");
	
	ПутьНастройки = "Тесты";
	Настройки(КонтекстЯдра, ПутьНастройки);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаборТестов(НаборТестов, КонтекстЯдра) Экспорт
	
	Если Не ВыполнятьТест(КонтекстЯдра) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОсновныеРоли) Тогда
		НаборТестов.НачатьГруппу("Основные роли конфигурации", Ложь);
		Для Каждого ОсновнаяРоль Из ОсновныеРоли Цикл
			ИмяПроцедуры = "ТестДолжен_ПроверитьСоставОсновныхРолей";
			НаборТестов.Добавить(ИмяПроцедуры, НаборТестов.ПараметрыТеста(ОсновнаяРоль), ОсновнаяРоль);	
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтандартныеРоли) Тогда
		НаборТестов.НачатьГруппу("Стандартные роли", Ложь);
		Для Каждого СтандартнаяРоль Из СтандартныеРоли Цикл
			ИмяПроцедуры = "ТестДолжен_ПроверитьПраваДоступаСтандартнойРоли";
			НаборТестов.Добавить(
				ИмяПроцедуры, 
				НаборТестов.ПараметрыТеста(СтандартнаяРоль.Ключ, СтандартнаяРоль.Значение), 
				СтандартнаяРоль.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	НаборТестов.НачатьГруппу("Роли", Ложь);
	ИменаРолей = ИменаРолей(СтандартныеРоли);	
	Для Каждого ИменаРоли Из ИменаРолей Цикл
		ИмяПроцедуры = "ТестДолжен_ПроверитьНедоступностьПравНаКореньКонфигурацииДляОстальныхРолей";
		НаборТестов.Добавить(ИмяПроцедуры, НаборТестов.ПараметрыТеста(ИменаРоли.Имя, ИменаРоли.ПолноеИмя), ИменаРоли.Имя);	
	КонецЦикла;
			
КонецПроцедуры

#КонецОбласти

#Область РаботаСНастройками

&НаКлиенте
Процедура Настройки(КонтекстЯдра, Знач ПутьНастройки)

	Если ЗначениеЗаполнено(Объект.Настройки) Тогда
		Возврат;
	КонецЕсли;
	
	ИсключенияИзПроверок = Новый Соответствие;
	ПлагинНастроек = КонтекстЯдра.Плагин("Настройки");
	Объект.Настройки = ПлагинНастроек.ПолучитьНастройку(ПутьНастройки);
	Настройки = Объект.Настройки;
	
	Если Не ЗначениеЗаполнено(Объект.Настройки) Тогда
		Объект.Настройки = Новый Структура(ПутьНастройки, Неопределено);
		Возврат;
	КонецЕсли;
	
	Если Настройки.Свойство(ИмяТеста()) И Настройки[ИмяТеста()].Свойство("ОсновныеРоли") Тогда
		ОсновныеРоли = Настройки[ИмяТеста()].ОсновныеРоли;		
	КонецЕсли;
	
	Если Настройки.Свойство(ИмяТеста()) И Настройки[ИмяТеста()].Свойство("СтандартныеРоли") Тогда
		СтандартныеРоли = Настройки[ИмяТеста()].СтандартныеРоли;		
	КонецЕсли;
	
	Если Настройки.Свойство(ИмяТеста()) И Настройки[ИмяТеста()].Свойство("ИсключенияИзПроверок") Тогда
		ИсключенияИзПроверок(Настройки);
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ИсключенияИзПроверок(Настройки)

	Для Каждого ИсключенияИзПроверокПоОбъектам Из Настройки[ИмяТеста()].ИсключенияИзпроверок Цикл
		Для Каждого ИсключениеИзПроверок Из ИсключенияИзПроверокПоОбъектам.Значение Цикл
			ИсключенияИзПроверок.Вставить(ВРег(ИсключенияИзПроверокПоОбъектам.Ключ + "." + ИсключениеИзПроверок), Истина); 	
		КонецЦикла;
	КонецЦикла;	

КонецПроцедуры

#КонецОбласти

#Область Тесты

&НаКлиенте
Процедура ТестДолжен_ПроверитьСоставОсновныхРолей(ИмяРоли) Экспорт
		
	Результат = ПроверитьСоставОсновныхРолей(ИмяРоли);
	ШаблонОшибки = НСтр("ru = 'Роль %1 не входит в состав основных ролей конфигурации'");
	ТекстОшибки = СтроковыеУтилиты.ПодставитьПараметрыВСтроку(ШаблонОшибки, ИмяРоли);
	Утверждения.Проверить(Результат = Истина, ТекстОшибки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьСоставОсновныхРолей(ИмяРоли)
	
	Роль = Метаданные.Роли.Найти(ИмяРоли);	
	Результат = Метаданные.ОсновныеРоли.Содержит(Роль);
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ТестДолжен_ПроверитьПраваДоступаСтандартнойРоли(ИмяРоли, ПраваДоступаРоли) Экспорт
		
	Результат = ПроверитьПраваДоступаСтандартнойРоли(ИмяРоли, ПраваДоступаРоли);
	ШаблонОшибки = НСтр("ru = 'Для стандартной роли %1 не установлены права доступа: %2'");
	ТекстОшибки = СтроковыеУтилиты.ПодставитьПараметрыВСтроку(ШаблонОшибки, ИмяРоли, Результат);
	Утверждения.Проверить(Результат = "", ТекстОшибки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьПраваДоступаСтандартнойРоли(ИмяРоли, ПраваДоступаРоли)
	
	Результат = "";
	
	Роль = Метаданные.Роли.Найти(ИмяРоли);
	
	Для Каждого ПравоДоступаРоли Из ПраваДоступаРоли Цикл
		Если Не ПравоДоступа(ПравоДоступаРоли, Метаданные, Роль) Тогда
			Разделитель = ?(ЗначениеЗаполнено(Результат), ", ", "");
			Результат = Результат + Разделитель + ПравоДоступаРоли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ТестДолжен_ПроверитьНедоступностьПравНаКореньКонфигурацииДляОстальныхРолей(ИмяРоли, ПолноеИмяРоли) Экспорт
	
	ПропускатьТест = ПропускатьТест(ИмяРоли, ПолноеИмяРоли);	
	
	Результат = ПроверитьНедоступностьПравНаКореньКонфигурацииДляОстальныхРолей(ИмяРоли);	
	Если Результат <> "" И ПропускатьТест.Пропустить Тогда
		Утверждения.ПропуститьТест(ПропускатьТест.ТекстСообщения);
	Иначе
		Утверждения.Проверить(Результат = "", ТекстСообщения(ИмяРоли, Результат));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьНедоступностьПравНаКореньКонфигурацииДляОстальныхРолей(ИмяРоли)
	
	Результат = "";
	
	Роль = Метаданные.Роли.Найти(ИмяРоли);
	ПраваДоступаДляПроверки = ПраваДоступаДляПроверки();
	
	Для Каждого ПравоДоступаДляПроверки Из ПраваДоступаДляПроверки Цикл
		Если ПравоДоступа(ПравоДоступаДляПроверки, Метаданные, Роль) Тогда
			Разделитель = ?(ЗначениеЗаполнено(Результат), ", ", "");
			Результат = Результат + Разделитель + ПравоДоступаДляПроверки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПропускатьТест(ИмяРоли, ПолноеИмяРоли)

	Результат = Новый Структура;
	Результат.Вставить("ТекстСообщения", "");
	Результат.Вставить("Пропустить", Ложь);
	
	Если ИсключенияИзПроверок.Получить(ВРег(ПолноеИмяРоли)) <> Неопределено Тогда
		ШаблонСообщения = НСтр("ru = 'Роль ""%1"" исключен из проверки'");
		Результат.ТекстСообщения = СтроковыеУтилиты.ПодставитьПараметрыВСтроку(ШаблонСообщения, ИмяРоли);
		Результат.Пропустить = Истина;
		Возврат Результат;
	КонецЕсли;
		
	Возврат Результат;

КонецФункции 

&НаКлиенте
Функция ТекстСообщения(ИмяРоли, Результат)

	ШаблонСообщения = НСтр("ru = 'Для роли %1 установлены права доступа на корень конфигурации: %2'");
	ТекстСообщения = СтроковыеУтилиты.ПодставитьПараметрыВСтроку(ШаблонСообщения, ИмяРоли, Результат);
	
	Возврат ТекстСообщения;

КонецФункции

&НаСервере
Функция ИмяТеста()
	Возврат РеквизитФормыВЗначение("Объект").Метаданные().Имя;
КонецФункции

&НаКлиенте
Функция ВыполнятьТест(КонтекстЯдра)
	
	ВыполнятьТест = Истина;
	ПутьНастройки = "Тесты";
	Настройки(КонтекстЯдра, ПутьНастройки);
	Настройки = Объект.Настройки;
	
	Если Не ЗначениеЗаполнено(Настройки) Тогда
		Возврат ВыполнятьТест;
	КонецЕсли;
		
	Если ТипЗнч(Настройки) = Тип("Структура") 
		И Настройки.Свойство("Параметры") 
		И Настройки.Параметры.Свойство(ИмяТеста()) Тогда
		ВыполнятьТест = Настройки.Параметры[ИмяТеста()];	
	КонецЕсли;
	
	Возврат ВыполнятьТест;

КонецФункции

&НаСервереБезКонтекста
Функция ИменаРолей(СтандартныеРоли)

	ИменаРолей = Новый Массив;
	
	Для Каждого Роль Из Метаданные.Роли Цикл
		Если Метаданные.ОсновныеРоли.Содержит(Роль) Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(СтандартныеРоли) = Тип("Структура") И СтандартныеРоли.Свойство(Роль.Имя) Тогда
			Продолжить;
		КонецЕсли;
		СтруктураРоли = Новый Структура;
		СтруктураРоли.Вставить("Имя", Роль.Имя);
		СтруктураРоли.Вставить("ПолноеИмя", Роль.ПолноеИмя());
		ИменаРолей.Добавить(СтруктураРоли);
	КонецЦикла;	
	
	Возврат ИменаРолей;

КонецФункции 

&НаСервереБезКонтекста
Функция ПраваДоступаДляПроверки()

	ПраваДоступаДляПроверки = Новый Массив;
		
	ПраваДоступаДляПроверки.Добавить("Администрирование");
	ПраваДоступаДляПроверки.Добавить("АдминистрированиеДанных");
	ПраваДоступаДляПроверки.Добавить("ОбновлениеКонфигурацииБазыДанных");
	ПраваДоступаДляПроверки.Добавить("МонопольныйРежим");
	ПраваДоступаДляПроверки.Добавить("АктивныеПользователи");
	ПраваДоступаДляПроверки.Добавить("ЖурналРегистрации");
	ПраваДоступаДляПроверки.Добавить("ТонкийКлиент");
	ПраваДоступаДляПроверки.Добавить("ВебКлиент");
	ПраваДоступаДляПроверки.Добавить("МобильныйКлиент");
	ПраваДоступаДляПроверки.Добавить("ТолстыйКлиент");
	ПраваДоступаДляПроверки.Добавить("ВнешнееСоединение");
	ПраваДоступаДляПроверки.Добавить("Automation");
	ПраваДоступаДляПроверки.Добавить("РежимВсеФункции");
	ПраваДоступаДляПроверки.Добавить("РегистрацияИнформационнойБазыСистемыВзаимодействия");
	ПраваДоступаДляПроверки.Добавить("СохранениеДанныхПользователя");
	ПраваДоступаДляПроверки.Добавить("АдминистрированиеРасширенийКонфигурации");
	ПраваДоступаДляПроверки.Добавить("ИнтерактивноеОткрытиеВнешнихОбработок");
	ПраваДоступаДляПроверки.Добавить("ИнтерактивноеОткрытиеВнешнихОтчетов");
	ПраваДоступаДляПроверки.Добавить("Вывод");
	
	Возврат ПраваДоступаДляПроверки;

КонецФункции 

#КонецОбласти