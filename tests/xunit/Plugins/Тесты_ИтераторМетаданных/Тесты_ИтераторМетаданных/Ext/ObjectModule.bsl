
Перем КонтекстЯдра;
Перем Ожидаем;
Перем ИтераторМетаданных;

Перем КоличествоВызововТипа;
Перем КоличествоВызововОбъекта;

#Область Стандартный_интерфейс

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	ИтераторМетаданных = КонтекстЯдра.Плагин("ИтераторМетаданных");
	
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов, КонтекстЯдраПараметр) Экспорт
	
	НаборТестов.Добавить("Тест_Инициализация");
	НаборТестов.Добавить("Тест_ЗаполнениеДереваПоВсемОбъектам");
	НаборТестов.Добавить("Тест_ЗаполнениеДереваПоВсемДокументам");
	НаборТестов.Добавить("Тест_ЗаполнениеДереваПоВсемДокументамИЗависимым");
	НаборТестов.Добавить("Тест_ПроверкаИтерацийПеречисления");
	//НаборТестов.Добавить("");
	
КонецПроцедуры

Процедура ПередЗапускомТеста() Экспорт

КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт

КонецПроцедуры

#КонецОбласти

#Область Тесты

Процедура Тест_Инициализация() Экспорт
	
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Неопределено);
	ИтераторМетаданных.ИсключаемыеМетаданные.Добавить(Неопределено);
	ИтераторМетаданных.ДополнятьЗависимымиОбъектами = Истина;
	
	Ожидаем.Что(ИтераторМетаданных.ДопустимыеМетаданные.Количество()>0).ЕстьИстина();
	Ожидаем.Что(ИтераторМетаданных.ИсключаемыеМетаданные.Количество()>0).ЕстьИстина();
	Ожидаем.Что(ИтераторМетаданных.ДополнятьЗависимымиОбъектами=Истина).ЕстьИстина();
	
	ИтераторМетаданных.Инициализация(КонтекстЯдра);
	
	Ожидаем.Что(ИтераторМетаданных.ДопустимыеМетаданные.Количество()=0).ЕстьИстина();
	Ожидаем.Что(ИтераторМетаданных.ИсключаемыеМетаданные.Количество()=0).ЕстьИстина();
	Ожидаем.Что(ИтераторМетаданных.ДополнятьЗависимымиОбъектами=Ложь).ЕстьИстина();
	
КонецПроцедуры

Процедура Тест_ЗаполнениеДереваПоВсемОбъектам() Экспорт
	
	ИтераторМетаданных.Инициализация(КонтекстЯдра);
	Дерево = ИтераторМетаданных.ДеревоМетаданных();
	Ожидаем.Что(Дерево.Строки.Количество()>0).ЕстьИстина();
	
КонецПроцедуры

Процедура Тест_ЗаполнениеДереваПоВсемДокументам() Экспорт
	
	ИтераторМетаданных.Инициализация(КонтекстЯдра);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.Документы);
	Дерево = ИтераторМетаданных.ДеревоМетаданных();
	
	Ожидаем.Что(Дерево.Строки.Количество()=1).ЕстьИстина();
	Ожидаем.Что(Дерево.Строки[0].Строки.Количество()).Равно(Метаданные.Документы.Количество());
	
	ИтераторМетаданных.ИсключаемыеМетаданные.Добавить(Метаданные.Документы[0]);
	Дерево = ИтераторМетаданных.ДеревоМетаданных();
	
	Ожидаем.Что(Дерево.Строки.Количество()=1).ЕстьИстина();
	Ожидаем.Что(Дерево.Строки[0].Строки.Количество()).Равно(Метаданные.Документы.Количество() - 1);
	
КонецПроцедуры

Процедура Тест_ЗаполнениеДереваПоВсемДокументамИЗависимым() Экспорт
	
	ИтераторМетаданных.Инициализация(КонтекстЯдра);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.Документы);
	ИтераторМетаданных.ДополнятьЗависимымиОбъектами = Истина;
	Дерево = ИтераторМетаданных.ДеревоМетаданных();
	
	Ожидаем.Что(Дерево.Строки.Количество()).Больше(1);
	
КонецПроцедуры

Процедура Тест_ПроверкаИтерацийПеречисления() Экспорт
	
	КоличествоВызововТипа = 0;
	КоличествоВызововОбъекта = 0;
	
	ИтераторМетаданных.Инициализация(КонтекстЯдра);
	ИтераторМетаданных.ДопустимыеМетаданные.Добавить(Метаданные.Документы);
	
	ИтераторМетаданных.Перечислить(ЭтотОбъект, "ОбработчикСобытияНовогоОбъекта", "ОбработчикСобытияНовогоТипа");
	Ожидаем.Что(КоличествоВызововТипа).Равно(1);
	Ожидаем.Что(КоличествоВызововОбъекта).Равно(Метаданные.Документы.Количество());
	
КонецПроцедуры

Процедура ОбработчикСобытияНовогоОбъекта(Объект, Родитель) Экспорт
	КоличествоВызововОбъекта = КоличествоВызововОбъекта + 1;
	Ожидаем.Что(Родитель).Заполнено();
	Ожидаем.Что(Объект.Родитель()).ИмеетТип(Тип("ОбъектМетаданныхКонфигурация"));
КонецПроцедуры

Процедура ОбработчикСобытияНовогоТипа(Объект, Родитель) Экспорт
	КоличествоВызововТипа = КоличествоВызововТипа + 1;
	Ожидаем.Что(Родитель).Не_().Заполнено();
	Ожидаем.Что(Объект).ИмеетТип(Тип("Строка"));
КонецПроцедуры

#КонецОбласти

////////////////////////////
// Тесты

